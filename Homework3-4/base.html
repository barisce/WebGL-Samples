<!-- Baris Celik - N18136522 -->
<!doctype html>
<html>
	<head>
		<title>Baris Celik</title>
		<style>
			body{ background-color: grey; }
			canvas{ background-color: white; }
		</style>
		<script src="raf_polyfill.js"></script>
		<script src="https://cdnjs.cloudflare.com/ajax/libs/gl-matrix/2.4.0/gl-matrix-min.js"></script>
		<script src="https://cdnjs.cloudflare.com/ajax/libs/mathjs/3.16.4/math.js"></script>
		<script src="webgl-obj-loader.js"></script>

		<script id="directionalLight-vs" type="x-shader/x-vertex">
			attribute vec3 aVertexPosition;
			attribute vec3 aVertexNormal;	
			
			varying highp vec4 vColor;	

			uniform mat4 uMVMatrix;
			uniform mat4 uPMatrix;
			uniform mat4 uMVITMatrix;

			uniform vec3 uInverseLightDirection;
			uniform float uShininess;
			uniform vec4 uDiffuseProduct;
			uniform vec4 uSpecularProduct;
			uniform vec4 uAmbientProduct;

			void main(void){
				vec3 posInCameraSpace = (uMVMatrix * vec4(aVertexPosition, 1.0)).xyz;
				vec3 l = normalize(uInverseLightDirection);
				vec3 n = normalize((uMVITMatrix * vec4(aVertexNormal, 0.0)).xyz);

				vec3 v = normalize(-posInCameraSpace);
				float nl = dot(n,l);
				vec3 r = normalize(2.0*nl*n-l);

				vec4 diffuse = max(nl,0.0)*uDiffuseProduct;
				vec4 specular = pow(max(dot(r,v),0.0),uShininess)*uSpecularProduct;
				vec4 ambient = uAmbientProduct;

				if ( nl <= 0.0 ) { //Not Illuminated
					vColor = vec4(0,0,0,1);
				} else {
					vColor = diffuse + ambient + specular;
					vColor.a = 1.0;
				}

				gl_Position = uPMatrix * uMVMatrix * vec4(aVertexPosition, 1.0);
			}
		</script>

		<script id="directionalLight-fs" type="x-shader/x-fragment">
			varying highp vec4 vColor;

			void main(void) {
				gl_FragColor = vColor;
			}

		</script>

		<script id="directionalLight2-vs" type="x-shader/x-vertex">
			attribute vec3 aVertexPosition;
			attribute vec3 aVertexNormal;	
			
			uniform mat4 uMVMatrix;
			uniform mat4 uPMatrix;
			uniform mat4 uMVITMatrix;

			varying vec3 n, v;
			
			
			void main(void){

				vec3 posInCameraSpace = (uMVMatrix * vec4(aVertexPosition, 1.0)).xyz;

				n = (uMVITMatrix * vec4(aVertexNormal, 0.0)).xyz;

				v = -posInCameraSpace;
				
				gl_Position = uPMatrix * uMVMatrix * vec4(aVertexPosition, 1.0);
				
			}

			
		</script>

		<script id="directionalLight2-fs" type="x-shader/x-fragment">
		precision mediump float;
		
		uniform  float uShininess;
		uniform  vec4 uDiffuseProduct;
		uniform  vec4 uSpecularProduct;
		uniform  vec4 uAmbientProduct;

		uniform vec3 uInverseLightDirection;		

		//Varying variables provide an interface between Vertex and Fragment Shader. 
		//Vertex Shaders compute values per vertex and fragment shaders compute values per fragment.
		// If you define a varying variable in a vertex shader, 
		//its value will be interpolated (perspective-correct) over the primitive being rendered 
		//and you can access the interpolated value in the fragment shader.
		
		varying vec3 n, v;

		void main(void){

			//Note that we could normalize in the vertex shader but because
			// it's a varying it will be linearly interpolated between our positions 
			//and so would not be a complete unit vector

			 vec3 l = normalize(uInverseLightDirection);
			 vec3 n2 = normalize(n);
			 vec3 v2 = normalize(v);

			 float nl = dot(n2,l);
			 vec3 r = normalize(2.0*nl*n2-l);

			 vec4 diffuse = max(nl,0.0)*uDiffuseProduct;
			 vec4 specular = pow(max(dot(r,v2),0.0),uShininess)*uSpecularProduct;
			 vec4 ambient = uAmbientProduct;

			vec4 fColor = diffuse + ambient + specular;
			fColor.a = 1.0;

			gl_FragColor = fColor;

		}
		</script>

		<script id="pointLight-vs" type="x-shader/x-vertex">
			attribute vec3 aVertexPosition;
			attribute vec3 aVertexNormal;

			varying highp vec4 vColor;

			uniform mat4 uMVMatrix;
			uniform mat4 uPMatrix;
			uniform mat4 uMVITMatrix;

			uniform vec3 uLightPosition;
			uniform float uAttenuation;

			uniform float uShininess;
			uniform vec4 uDiffuseProduct;
			uniform vec4 uSpecularProduct;
			uniform vec4 uAmbientProduct;

			void main(void){
				vec3 posInCameraSpace = (uMVMatrix * vec4(aVertexPosition, 1.0)).xyz;

				vec3 l = normalize( uLightPosition.xyz - posInCameraSpace );
				vec3 n = normalize((uMVITMatrix * vec4(aVertexNormal, 0.0)).xyz);

				// if attenuation stays 1.0 then its directional light and goes on forever
				float attenuation = 1.0;
				float distanceToLight = length(uLightPosition.xyz - posInCameraSpace);
				// as distance increases, decrease the light that it receives
				attenuation = 1.0 / (1.0 + uAttenuation * (pow(distanceToLight, 2.0)));

				vec3 v = normalize(-posInCameraSpace);
				float nl = dot(n,l);
				vec3 r = normalize(2.0*nl*n-l);

				vec4 diffuse = 0.8 * max(nl,0.0)*uDiffuseProduct;
				vec4 specular = 0.4 * pow(max(dot(r,v),0.0),uShininess)*uSpecularProduct;
				vec4 ambient = uAmbientProduct;

				if ( nl <= 0.0 ) { //Not Illuminated
					vColor = ambient;
				} else {
					if (dot(r,v) > 0.0) {
						// make the surface glow
						vColor = ambient +  attenuation*(diffuse + specular);
					} else {
						vColor = attenuation*diffuse + ambient;
					}
				}
				vColor.a = 1.0;

				gl_Position = uPMatrix * uMVMatrix * vec4(aVertexPosition, 1.0);
			}

		</script>

		<script id="pointLight-fs" type="x-shader/x-fragment">
			varying highp vec4 vColor;

			void main(void) {
				gl_FragColor = vColor;
			}

		</script>


		<script id="pointLight2-vs" type="x-shader/x-vertex">
			attribute vec3 aVertexPosition;
			attribute vec3 aVertexNormal;

			uniform mat4 uMVMatrix;
			uniform mat4 uPMatrix;
			uniform mat4 uMVITMatrix;

			varying vec3 n, v;

			void main(void){
				vec3 posInCameraSpace = (uMVMatrix * vec4(aVertexPosition, 1.0)).xyz;

				n = (uMVITMatrix * vec4(aVertexNormal, 0.0)).xyz;

				v = -posInCameraSpace;

				gl_Position = uPMatrix * uMVMatrix * vec4(aVertexPosition, 1.0);
			}
		</script>

		<script id="pointLight2-fs" type="x-shader/x-fragment">
			precision mediump float;

			uniform  float uShininess;
			uniform  vec4 uDiffuseProduct;
			uniform  vec4 uSpecularProduct;
			uniform  vec4 uAmbientProduct;

			// Light Position holds the point lights poisition
			uniform vec3 uLightPosition;
			// Attenuation holds the range of the light
			uniform float uAttenuation;

			varying vec3 n, v;

			void main(void) {

				vec3 n2 = normalize(n);
				vec3 v2 = normalize(v);
				vec3 l = normalize(uLightPosition.xyz + v2);

				// if attenuation stays 1.0 then its directional light and goes on forever
				float attenuation = 1.0;
				float distanceToLight = length(uLightPosition.xyz + v);
				// as distance increases, decrease the light that it receives
				attenuation = 1.0 / (1.0 + uAttenuation * (pow(distanceToLight, 2.0)));

				float nl = dot(n2,l);
				vec3 r = normalize(2.0*nl*n2-l);

				vec4 diffuse = max(nl,0.0)*uDiffuseProduct;
				vec4 specular = pow(max(dot(r,v2),0.0),uShininess)*uSpecularProduct;
				vec4 ambient = uAmbientProduct;

				vec4 fColor;

				if ( nl <= 0.0 ) { //Not Illuminated
					fColor = ambient;
				} else {
					if (dot(r,v2) > 0.0) {
						// make the surface glow
						fColor = ambient +  attenuation*(diffuse + specular);
					} else {
						fColor = attenuation*diffuse + ambient;
					}
				}
				fColor.a = 1.0;

				//multiply light value with color
				gl_FragColor = fColor;
			}

		</script>

		<script src="Camera.js"></script>
		<script src="Texture.js"></script>
		<script src="Program.js"></script>
		<script src="Light.js"></script>
		<script src="Material.js"></script>
		<script src="Mesh.js"></script>
		<script src="SceneObject.js"></script>
		<script src="UI.js"></script>
		<script src="Input.js"></script>
		<script src="Main.js"></script>		

		<script>
			var gl = null,
				canvas = null;
			
			function initWebGL(){
				canvas = document.getElementById("my-canvas");  
				try{
					gl = canvas.getContext("webgl") || canvas.getContext("experimental-webgl");					
				}catch(e){
				}
								
				if(gl){
					initInput();
					window.init();

					//just call once to start updating
					loop();
					
				}else{	
					alert(  "Error: Your browser does not appear to support WebGL.");
				}
			}

			function loop(){
				window.update();

				//setTimeout(loop, 0);
				//request a new update causes loop
				requestAnimationFrame(loop, canvas);
			}			
		</script>
	</head>
	<body onload="initWebGL()">
		<canvas id="my-canvas" width="800" height="600">
			Your browser does not support the HTML5 canvas element.
		</canvas>
		<!--<div id="lightPos"><strong>-&#45;&#45;</strong></div>-->
		<div id="ui">

		</div>
	</body>
</html>
